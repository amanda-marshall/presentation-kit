sub
  showHeartbeatWithPicture(
    bool shouldGiveHeartbeatFeedback, # Allow disabling of sound feedback
    int count, # For how many heartbeats will the stimulus be presnted?
    int delay, # Millisecond delay to apply to the sound feedback
    picture graphic, # The graphic to present while the trial is showing
    string triggerName, # Trigger to send at the beginning of the trial
    string triggerNameFeedback # Trigger to send when heartbeat feedback is given
  )
begin

  # Create 10ms 1000Hz Sine Signal at 75Db
  wavefile sine_wave = new wavefile(new asg::sine(10.0, 1000.0, 0.0));
  sound beep = new sound(sine_wave);
  beep.set_attenuation(0.25);

  # Define graphic to show
  picture p = graphic;

  trial t = new trial();
  t.add_stimulus_event(p);

  # Send trigger at start of presentation
  sendTrigger(triggerName);

  # Check if input port was set up
	bool has_ports = input_port_manager.port_count() > 0;
	if !has_ports then exit("No input port for heart rate triggers defined"); end;

	input_port port = input_port_manager.get_port(1);
	port.clear();

  loop
    int timeNow;
    int timeStart = clock.time();
    bool timeOut = false;
    int beepsPresented = 0;
    int timeSinceLastBeep = timeStart - 400;
    array<int> pendingBeeps[0];
  until
    beepsPresented > count || timeOut
  begin

    # Present each frame about 60 times per second
    t.set_duration(17);
    timeNow = clock.time();

    # End trial if there isn't a single heartbeat detected after 2s
    timeOut = (beepsPresented == 0 && timeNow-timeStart > 2000);

    # Go through new port codes for heart beat detection
    loop int i = 1; until i > port.total_count()
		begin
      if port.codes(i) == 255 then
        # Enforce minimum delay between recorded beeps
        if timeNow - timeSinceLastBeep >= 200  then
          pendingBeeps.add(timeNow + delay);
          timeSinceLastBeep = timeNow;
        end;
        break;
		  end;
			i = i + 1;
		end;

    # Play sounds after a delay
    int pendingBeepsCount = pendingBeeps.count();
    if pendingBeepsCount > beepsPresented then

      # Check if first queued, but unplayed heartbeat should be played
      if timeNow >= pendingBeeps[beepsPresented+1] then

        sendTrigger(triggerNameFeedback);

        # Don't exceed maximum beep count
        if shouldGiveHeartbeatFeedback && beepsPresented < count then
          beep.present();
        end;

        # Make sure trial is long enough to play whole sound
        t.set_duration(100);
        beepsPresented = beepsPresented + 1;

      end;
    end;

    port.clear();
    t.present();

  end;

end;

sub
  showHeartbeat(
    bool shouldGiveHeartbeatFeedback,
    int count,
    int delay,
    string triggerName,
    string triggerNameFeedback
  )
begin
  picture graphic = getLineGraphic("heartbeat", 1.0, false);
  showHeartbeatWithPicture(
    shouldGiveHeartbeatFeedback,
    count,
    delay,
    graphic,
    triggerName,
    triggerNameFeedback
  );
end;
